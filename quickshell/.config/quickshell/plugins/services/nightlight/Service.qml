import QtQuick
import Quickshell.Io
import "NightlightModel.js" as NightlightModel

Item {
  id: root

  // Injected by omarchy-shell (the first-party service loader).
  property var shell: null

  // Keep in sync with bin/omarchy-toggle-nightlight, which sets the same
  // temperatures for callers outside the shell (keybindings, menu, ssh).
  readonly property int nightTemperature: 4000
  readonly property int dayTemperature: 6500

  property bool stateLoaded: false
  property var temperature: null
  readonly property bool enabled: stateLoaded && NightlightModel.isNightlight(temperature)

  property bool hasPendingTemperature: false
  property int pendingTemperature: 0

  function refresh() {
    if (!statusProbe.running) statusProbe.running = true
  }

  function setNightlight(value) {
    applyTemperature(value ? nightTemperature : dayTemperature)
  }

  function toggle() {
    setNightlight(!enabled)
  }

  function applyTemperature(temp) {
    root.temperature = temp
    root.stateLoaded = true

    if (applyProcess.running) {
      root.pendingTemperature = temp
      root.hasPendingTemperature = true
      return
    }

    runApply(temp)
  }

  function runApply(temp) {
    // wlsunset has no temperature-setting IPC. It is controlled with SIGUSR1:
    // automatic -> day/high -> night/low -> automatic. Restarting it here
    // gives the shell deterministic ownership of the forced state.
    var signals = Number(temp) < NightlightModel.IDENTITY_TEMPERATURE ? 2 : 1
    applyProcess.command = ["bash", "-lc",
      "pkill -x wlsunset 2>/dev/null || true; " +
      "setsid wlsunset -t " + root.nightTemperature + " -T " + root.dayTemperature + " -S 00:00 -s 00:01 -d 1 >/dev/null 2>&1 & " +
      "pid=$!; sleep 0.25; " +
      "i=0; while [ $i -lt " + signals + " ]; do kill -USR1 \"$pid\" || exit 1; i=$((i+1)); sleep 0.05; done; " +
      "echo \"$pid\""]
    applyProcess.running = true
  }

  Process {
    id: statusProbe
    command: ["bash", "-lc", "pgrep -xo wlsunset"]
    stdout: StdioCollector {
      waitForEnd: true
      onStreamFinished: {
        root.stateLoaded = true
        if (String(text).trim() === "") {
          root.temperature = null
        } else if (root.temperature === null || root.temperature === undefined) {
          root.temperature = root.dayTemperature
        }
      }
    }
    onExited: function(exitCode) {
      if (exitCode !== 0) {
        root.temperature = null
      }
      root.stateLoaded = true
    }
  }

  Process {
    id: applyProcess
    onExited: function() {
      if (root.hasPendingTemperature) {
        root.hasPendingTemperature = false
        root.runApply(root.pendingTemperature)
        return
      }

      root.refresh()
    }
  }

  Component.onCompleted: refresh()

  IpcHandler {
    target: "nightlight"

    function status(): string {
      return JSON.stringify({ enabled: root.enabled, temperature: root.temperature })
    }

    function refresh(): void {
      root.refresh()
    }

    function enable(): string {
      root.setNightlight(true)
      return "enabled"
    }

    function disable(): string {
      root.setNightlight(false)
      return "disabled"
    }

    function toggle(): string {
      var enabling = !root.enabled
      root.setNightlight(enabling)
      return enabling ? "enabled" : "disabled"
    }
  }
}
